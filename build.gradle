plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '6.0.+'
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

version = '1.20.1-2.15.11-forge-dev'
group = 'me.jddev0.energizedpower' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'energizedpower'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // official   MCVersion             Official field/method names from Mojang mapping files
    // parchment  YYYY.MM.DD-MCVersion  Open community-sourced parameter names and javadocs layered on top of official
    //
    // You must be aware of the Mojang license when using the 'official' or 'parchment' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Parchment is an unofficial project maintained by ParchmentMC, separate from MinecraftForge
    // Additional setup is needed to use their mappings: https://github.com/ParchmentMC/Parchment/wiki/Getting-Started
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'official', version: '1.20.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', 'energizedpower'

            // Fix mixin dependencies
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                energizedpower {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'energizedpower'

            // Fix mixin dependencies
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            // Disable Server GUI
            arg '--nogui'

            mods {
                energizedpower {
                    source sourceSets.main
                }
            }
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'energizedpower'

            // Fix mixin dependencies
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                energizedpower {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'energizedpower', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            // Fix mixin dependencies
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                energizedpower {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://modmaven.dev" }
    maven { url "https://maven.theillusivec4.top/" }
    maven { url "https://maven.terraformersmc.com/" }

    maven {
        url "https://squiddev.cc/maven/"
        content {
            includeGroup("cc.tweaked")
        }
    }

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so:
    flatDir {
        dir 'libs'
    }
}

dependencies {
    // Specify the version of Minecraft to use. If this is any group other than 'net.minecraft', it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency, and its patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:1.20.1-47.1.0'

    // Real mod deobf dependency examples - these get remapped to your current mappings
    // implementation fg.deobf("com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}") // Adds registrate as a dependency
    // REI integration
    compileOnly 'me.shedaniel.cloth:basic-math:0.6.1'
    compileOnly fg.deobf("dev.architectury:architectury-forge:9.0.7")
    compileOnly fg.deobf('me.shedaniel:RoughlyEnoughItems-forge:12.0.634')
    compileOnly "me.shedaniel:REIPluginCompatibilities-forge-annotations:12.+"

    // JEI integration
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-common-api:15.2.0.23")
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-forge-api:15.2.0.23")

    // EMI integration
    compileOnly fg.deobf("dev.emi:emi-forge:1.0.19+1.20.1:api")

    // Curios integration
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.2.0-beta.3+1.20.1:api")

    // CCTweaked integration
    compileOnly "cc.tweaked:cc-tweaked-1.20.1-core-api:1.111.0"
    compileOnly fg.deobf("cc.tweaked:cc-tweaked-1.20.1-forge-api:1.111.0")

    // Examples using mod jars from ./libs
    // implementation fg.deobf("blank:coolmod-${mc_version}:${coolmod_version}")

    // For more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "energizedpower",
                "Specification-Vendor"    : "JDDev0",
                "Specification-Version"   : project.version,
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "JDDev0",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Example configuration to allow publishing using the maven-publish plugin
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
// publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.publishUnified {
    dependsOn(tasks.build)
}

unifiedPublishing {
    project {
        displayName = "Energized Power - " + "1.20.1" + " - v" + project.version.split("-")[1] + " [Forge/NeoForge]"
        version = project.version
        changelog = new File("CHANGELOG.md").exists()?new File("CHANGELOG.md").getText("UTF-8"):""
        releaseType = "release"
        gameVersions = ["1.20.1"]
        gameLoaders = ["forge", "neoforge"]

        mainPublication.set(project.rootDir.toPath().resolve("build/libs").resolve(project.archivesBaseName + "-" + project.version + ".jar").toFile())

        relations {
            //No dependencies
        }

        if(project.hasProperty('MODRINTH_TOKEN')) {
            modrinth {
                token = getProperty('MODRINTH_TOKEN') as String
                id = "6pku8gW1"
            }
        }

        if(project.hasProperty('CURSEFORGE_TOKEN')) {
            curseforge {
                token = getProperty('CURSEFORGE_TOKEN') as String
                id = "782147"
            }
        }
    }
}

//Dummy task to fix post sync error caused by NeoGradle
tasks.register("idePostSync") {

}